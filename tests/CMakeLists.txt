
set(MPIEXEC_COMMAND${MPIEXEC_EXECUTABLE})

macro(add_gtensor_test name)
  add_executable(${name})
  target_gtensor_sources(${name} PRIVATE ${name}.cxx)
  target_link_libraries(${name} gtensor GTest::gtest_main)
  if (USE_GTEST_DISCOVER_TESTS)
    # Note: workaround discovery timeout on some Intel systems
    gtest_discover_tests(${name} EXEC_WRAPPER ${MPIEXEC_COMMAND}
                         DISCOVERY_TIMEOUT 600)
  else()
    gtest_add_tests(TARGET ${name} EXEC_WRAPPER ${MPIEXEC_COMMAND})
  endif()

  if (GTENSOR_TEST_DEBUG)
    target_compile_definitions(${name} PRIVATE GTENSOR_TEST_DEBUG)
    target_compile_options(${name} PRIVATE $<$<COMPILE_LANGUAGE:CUDA>: --keep>)
  endif()
endmacro()

if ("${GTENSOR_DEVICE}" STREQUAL "cuda")
  add_gtensor_test(test_thrust_ext)
endif()

add_gtensor_test(test_expression)
add_gtensor_test(test_helper)
add_gtensor_test(test_gtensor)
add_gtensor_test(test_gtensor_span)
add_gtensor_test(test_view)
add_gtensor_test(test_wip)
add_gtensor_test(test_adapt)
add_gtensor_test(test_device_ptr)
add_gtensor_test(test_gtensor_storage)
add_gtensor_test(test_complex)
add_gtensor_test(test_device_backend)
add_gtensor_test(test_launch)
add_gtensor_test(test_span)
add_gtensor_test(test_reductions)
add_gtensor_test(test_sarray)
add_gtensor_test(test_assign)
add_gtensor_test(test_space)
add_gtensor_test(test_stream)
add_gtensor_test(test_gtest_predicates)
add_gtensor_test(test_sparse)
add_gtensor_test(test_half)

if (GTENSOR_ENABLE_CLIB)
  add_executable(test_clib)
  target_gtensor_sources(test_clib PRIVATE test_clib.cxx)
  if (USE_GTEST_DISCOVER_TESTS)
    gtest_discover_tests(test_clib EXEC_WRAPPER ${MPIEXEC_COMMAND})
  else()
    gtest_add_tests(TARGET test_clib EXEC_WRAPPER ${MPIEXEC_COMMAND})
  endif()
  target_link_libraries(test_clib cgtensor GTest::gtest_main)
  if (GTENSOR_TEST_DEBUG)
    target_compile_definitions(test_clib PRIVATE GTENSOR_TEST_DEBUG)
  endif()
endif()

if (GTENSOR_ENABLE_BLAS)
  add_gtensor_test(test_blas)
  add_gtensor_test(test_lapack)
  add_gtensor_test(test_bandsolver)
  target_link_libraries(test_blas gtblas)
  target_link_libraries(test_lapack gtblas)
  target_link_libraries(test_bandsolver gtblas)
  if (GTENSOR_ENABLE_CLIB)
    add_gtensor_test(test_cblas)
    target_link_libraries(test_cblas cgtblas)
  endif()
  if (GTENSOR_ENABLE_SOLVER)
    add_gtensor_test(test_solver)
    target_link_libraries(test_solver gtsolver)
  endif()

endif()

if (GTENSOR_ENABLE_FFT)
  add_gtensor_test(test_fft)
  target_link_libraries(test_fft gtfft)
endif()
